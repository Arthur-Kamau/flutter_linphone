group 'app.storiez.plugins.flutter_linphone'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.3.50'
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

rootProject.allprojects {
    repositories {
        google()
        jcenter()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

configurations {
    customImpl.extendsFrom(implementation)
}

task linphoneSdkSource() {
    doLast {
        configurations.customImpl.getIncoming().each {
            it.getResolutionResult().allComponents.each {
                if (it.id.getDisplayName().contains("linphone-sdk-android")) {
                    println 'Linphone SDK used is ' + it.moduleVersion.version + ' from ' + it.properties["repositoryName"]
                }
            }
        }
    }
}

repositories {
    maven {
        name "linphone.org maven repository"
        url "https://linphone.org/maven_repository"
    }
}

//project.tasks['preBuild'].dependsOn 'getGitVersion'
project.tasks['preBuild'].dependsOn 'linphoneSdkSource'

android {
    compileSdkVersion 30

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        //added
        main.jni.srcDirs = []
        // Disables the default ndk-build call (with on-the-fly Android.mk files)
        main.jniLibs.srcDir 'src/main/libs'
    }
    defaultConfig {
        minSdkVersion 23
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.linphone:linphone-sdk-android:4.5+"
}